Namespace LanguageExtensions
    ''' <summary>
    ''' Several language method extensions used to create data relations
    ''' between two tables. 
    ''' </summary>
    ''' <remarks>
    ''' The idea is more to hide the code which creates the relations from
    ''' our regular code.
    ''' </remarks>
    Public Module DataRelationsExtensions
        ''' <summary>
        ''' Wrapper to create a master detail relationship
        ''' </summary>
        ''' <param name="dataSet">Populated DataSet</param>
        ''' <param name="MasterTableName">Parent table</param>
        ''' <param name="ChildTableName">Child table</param>
        ''' <param name="KeyColumn">Common field linking master to child</param>
        ''' <remarks>
        ''' Relationship name is generated by concatenating the master and child
        ''' table names together
        ''' </remarks>
        <DebuggerStepThrough()>
        <Runtime.CompilerServices.Extension()>
        Public Sub SetRelation(
                               dataSet As DataSet,
                               MasterTableName As String,
                               ChildTableName As String,
                               KeyColumn As String)

            dataSet.Relations.Add(
                New DataRelation(String.Concat(MasterTableName, ChildTableName),
                                 dataSet.Tables(MasterTableName).Columns(KeyColumn),
                                 dataSet.Tables(ChildTableName).Columns(KeyColumn)
                                 )
                )

        End Sub
        ''' <summary>
        ''' Wrapper to create a master detail relationship
        ''' </summary>
        ''' <param name="dataSet">Populated DataSet</param>
        ''' <param name="MasterTableName">Parent table</param>
        ''' <param name="ChildTableName">Child table</param>
        ''' <param name="KeyColumn">Common field linking master to child</param>
        ''' <param name="Name">Relationship name</param>
        ''' <remarks></remarks>
        <DebuggerStepThrough()>
        <Runtime.CompilerServices.Extension()>
        Public Sub SetRelation(dataSet As DataSet, MasterTableName As String, ChildTableName As String, KeyColumn As String, Name As String)
            dataSet.Relations.Add(
                New DataRelation(Name,
                                 dataSet.Tables(MasterTableName).Columns(KeyColumn),
                                 dataSet.Tables(ChildTableName).Columns(KeyColumn)
                                 )
                )
        End Sub

    End Module
End Namespace